apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

repositories {
    jcenter()
}

sourceCompatibility = '1.8' // -source
targetCompatibility = '1.8' // -target

mainClassName = 'aspx.viewstate.ViewStateDecoder'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    filesMatching ('**/*.properties') {
       expand(project.properties)
    }
}

clean.doFirst { 
    delete 'release';
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    
    from {
        configurations.runtimeClasspath.filter{ it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }

    destinationDir = file('release')
    version =  "v${release_version_major}"
}

task release(type: Zip, dependsOn: ['build']) {
    archiveBaseName ="${rootProject.name}_v${release_version_major}.${release_version_minor}"
    destinationDirectory = file("${projectDir}")
    from rootProject.rootDir
    include '*'
    include 'gradle/**'
    include 'image/**'
    include 'libs/**'
    include 'src/**'
    include 'release/**'
    exclude 'build'
    exclude '.git'
    exclude '.gradle'
    exclude '*.zip'
}

dependencies {
    // https://github.com/raise-isayan/BurpExtlib
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // https://mvnrepository.com/artifact/net.portswigger.burp.extender/burp-extender-api
    compileOnly 'net.portswigger.burp.extender:burp-extender-api:2.1'
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation 'com.google.code.gson:gson:2.8.6'
    // UnitTest
    // https://mvnrepository.com/artifact/net.portswigger.burp.extender/burp-extender-api
    testImplementation 'net.portswigger.burp.extender:burp-extender-api:2.1'
    testImplementation     'junit:junit:4.13'

}
