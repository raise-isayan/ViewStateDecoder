apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

repositories {
    mavenCentral()
}

sourceCompatibility = '11' // -source
targetCompatibility = '11' // -target

mainClassName = 'aspx.viewstate.ViewStateDecoder'
//mainClassName = 'yagura.view.ViewStateMainFrame'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching ('**/*.properties') {
       expand(project.properties)
       // naitive2ascii
       filter(org.apache.tools.ant.filters.EscapeUnicode)
    }
}

clean.doFirst {
    delete fileTree('release') {
      include '*.jar'
    }
}

jar {
    // Keep jar clean:
    exclude 'META-INF/**', 'about.html', 'module-info.class'

    manifest {
        attributes 'Main-Class': mainClassName
    }

    from {
        configurations.runtimeClasspath.filter{ it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }

    destinationDirectory = file('release')
    archiveVersion =  "v${release_version_major}"
}

task release(type: Zip, dependsOn: ['build']) {
    archiveBaseName ="${rootProject.name}_v${release_version_major}.${release_version_minor}"
    destinationDirectory = file("${projectDir}")
    from rootProject.rootDir
    include '*'
    include 'gradle/**'
    include 'image/**'
    include 'libs/**'
    include 'src/**'
    include 'release/**'
    exclude 'build'
    exclude '.git'
    exclude '.gradle'
    exclude '*.zip'
}

dependencies {
    // https://mvnrepository.com/artifact/net.portswigger.burp.extender/burp-extender-api
//    stand alone mode で実行させるため含める
    implementation 'net.portswigger.burp.extender:burp-extender-api:2.3'
    // https://github.com/raise-isayan/BurpExtlib
    implementation fileTree(dir: 'libs', include: ['*.jar'])
        // https://mvnrepository.com/artifact/com.google.code.gson/gson
        implementation 'com.google.code.gson:gson:2.8.9'
        // https://mvnrepository.com/artifact/com.googlecode.juniversalchardet/juniversalchardet
        implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    // UnitTest
    // https://mvnrepository.com/artifact/net.portswigger.burp.extender/burp-extender-api
    testImplementation 'net.portswigger.burp.extender:burp-extender-api:2.3'
    testImplementation 'junit:junit:4.13.2'
}
